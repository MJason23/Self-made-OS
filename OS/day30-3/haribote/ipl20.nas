;ļҪǣBIOS00ͷ1ݶڴУдμغĴ룡װвϵͳ̻Ӳжȡ
;ڴеضλãȻCPUõڴеʼλãͿʽʼвϵͳˣסˣļ̵ĵһУֻܹ512ֽ

; haribote-ipl
; TAB=4

;ֻ˶ȡ̶ݵڴУΪԺϵͳ»ȻǲûںдʲôݣҲ˵ǰڴеĶЩ
;ǱҪḶ́εĳɹʾϴһġ

CYLS	EQU		20				;ʾȡʮϢ

		ORG		0x7c00			; ָװصַ

; δǱ׼FAT12ʽרõĴ

		JMP		entry
		DB		0x90
		DB		"Haribote"		; ƿַ8ֽڣ
		DW		512				; ÿsectorĴСΪ512ֽڣ
		DB		1				; أclusterĴСΪ1
		DW		1				; FATʼλãһӵһʼ
		DB		2				; FATĸΪ2
		DW		224				; Ŀ¼ĴСһ224
		DW		2880			; ô̵ĴС2880Ϊ棬ÿ512ֽڣ1.44M
		DB		0xf0			; ̵ࣨ0xf0̵ĸʽ룬Ҫ֤
		DW		9				; FATĳȣ9
		DW		18				; 1ŵtrackм18
		DW		2				; ͷ2
		DD		0				; ÷0
		DD		2880			; дһδ̴С
		DB		0,0,0x29		; 岻̶
		DD		0xffffffff		; ǣ
		DB		"HariboteOS "	; ̵ƣ11ֽڣĻҪո
		DB		"FAT12   "		; ̸ʽƣ8ֽڣ
		RESB	18				; ȿճ18ֽ

; ģͨʼĴȻSIĴͣӣȻmsgεϢputloop趨õĸʽʾĻ

entry:
		MOV		AX,0			; ʼĴ
		MOV		SS,AX
		MOV		SP,0x7c00
		MOV		DS,AX
		
;day3ӵĴ

		MOV		AX,0x0820		;ҪѳصڴλãΪûöѣûʲôر
		MOV		ES,AX
		MOV 	CH,0			;0
		MOV 	DH,0			;ͷ0
		MOV 	CL,2			;2
		
readloop:
		MOV  	SI,0 			;¼ʧܴļĴΪ̺ܲɿ׶ȡʧܣƳԶظ
		
		
;ʧܣҪ¼ϵͳԼصһֳΪ˶һ
retry:
		MOV 	AH,0x02			;AH=0x02
		MOV 	AL,1			;1
		MOV 	BX,0
		MOV 	DL,0x00			;A
		INT 	0x13			;ôBIOSǰЩýΪ˽жǰ׼ÿĴն˳жӦ⺬壬ɲ鿴鱾P46
		JNC		next			;ûĻֱתnext
		ADD		SI,1			;ĻȶSI1ʾ1
		CMP		SI,5			;Ĵ5ξͲڼˣֱתerrorΣдʾ
		JAE		error			;ȽϽ5תerror
		MOV		AH,0x00			;AH=0ʱλϵͳ
		MOV		DL,0x00			;A
		INT 	0x13			;жֻأĴֵˣΪ˶ȡ̶õֵ
		JMP		retry			;صretryΣ¿ʼ
		
		
;nextһοʼȡӵ10һ
next:
		MOV		AX,ES			;ڴַ0x200ʵ512ֽڣһĴС
		ADD		AX,0x0020
		MOV		ES,AX			;ΪֱӶESĴADDҪAXĴн
		ADD		CL,1			;ҪԵǰһȡCLҪ1
		CMP		CL,18			;Ϊһ18Ҫ18бȽ
		JBE		readloop		;ȽϽֱ18Сߵڣ˵ûжȡ꣬ȡˣͼ½Уȡһͷ
		MOV		CL,1
		ADD		DH,1
		CMP		DH,2
		JB		readloop			;DH<2˵ͷľжȡ꣬صreadloopȡȡˣҪصͷȡһϢ
		MOV		DH,0
		ADD		CH,1			;ȡһϢ
		CMP		CH,CYLS			;CYLSǺ궨ֵ10ҲǱȽϵǰǷ˵10
		JB		readloop		;CH<10˵ûж10棬ôصreadloopȡ
		
		MOV		[0x0ff0],CH		;CYLSֵ洢ڴλãѴװݵĽַharibote.sysʾһ£һʱڴ
		JMP		0xc200			;תĲϵͳȥִĳΪòֳ򱻼صڴ0xc200һҪļ޽ӣת뵽Ĳϵͳ

;Ϣʾ֮ûҪʾϢֱʹCPUȴ
fin:
		HLT						; CPUֹͣȴָ
		JMP		fin				; ѭ
		
error:
		MOV		SI,msg
		
		
;һδΪһʽʾϢday3һĻͲҪputloopⲿִʵ
putloop:
		MOV		AL,[SI]
		ADD		SI,1			; SI1
		CMP		AL,0
		JE		fin
		MOV		AH,0x0e			; ʾһ
		MOV		BX,15			; ַָɫ
		INT		0x10			; ԿBIOS
		JMP		putloop

		
;Ϣʾ֣day3вʾhello worldǵִʱʾload errorȷʱʾϢ
msg:
		DB		0x0a, 0x0a		; 2
		DB		"load error"    ;ĻҪʾϢ
		DB		0x0a			; 
		DB		0

		RESB	0x7dfe-$		; ظ0ֱ0x7dfeλõڴ棬-$ǰλõڴλ

		DB		0x55, 0xaa      ;ֻһֽд0x55aabiosŻΪ
